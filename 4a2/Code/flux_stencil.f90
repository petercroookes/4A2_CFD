
      module flux_stencil

!     Packaging a subroutine in a module allows it to recieve the data
!     conveniently as assumed shape arrays
      
      contains

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

      subroutine sum_fluxes(av,flux_i,flux_j,area,prop,dcell)

!     This subroutine sums the fluxes into each cell, calculates the change in 
!     the cell property inside, distributes the change to the four nodes of the
!     cell and then adds it onto the flow property

!     Explicitly declare the required variables
      use types
      implicit none
      type(t_appvars), intent(in) :: av
      real, intent(in) :: flux_i(:,:), flux_j(:,:), area(:,:)
      real, intent(inout) :: prop(:,:)
      real, intent(out) :: dcell(:,:)
      real, dimension(size(prop,1),size(prop,2)) :: dnode
      integer :: ni, nj, i, j

!     Get the block size and store locally for convenience
      ni = size(prop,1); nj = size(prop,2)

!     Use the finite volume method to find the change in the variables "prop"
!     over the timestep "dt", save it in the array "dcell"
!     INSERT

      do i = 1, ni - 1
         do j = 1, nj - 1
            dcell(i,j) = (flux_i(i,j) + flux_j(i,j) - flux_i(i+1,j) - &
                 flux_j(i,j+1)) * (av%dt(i,j)/area(i,j))
         end do
      end do
      
!     Now distribute the changes equally to the four corners of each cell. Each 
!     interior grid point receives one quarter of the change from each of the 
!     four cells adjacent to it.
!     INSERT
      
      do i = 2, ni - 1
         do j = 2, nj - 1
            dnode(i,j) = 0.25 * (dcell(i,j) + dcell(i,j-1) + dcell(i-1,j) &
                 + dcell(i-1,j-1))
         end do
      end do
            
!     Bounding edge nodes do not have four adjacent cells and so must be treated
!     differently, they only recieve half the change from each of the two
!     adjacent cells. Distribute the changes for the "i = 1 & ni" edges as well
!     as the "j = 1 & nj" edges. 
!     INSERT

      do j = 2, nj - 1
         dnode(1,j) = 0.5 * (dcell(1,j) + dcell(1,j-1))
         dnode(ni,j) = 0.5 * (dcell(ni-1,j) + dcell(ni-1,j-1))
      end do

      do i  = 2, ni -1
         dnode(i,1) = 0.5 * (dcell(i,1) + dcell(i-1,1))
         dnode(i,nj) = 0.5 * (dcell(i,nj-1) + dcell(i-1,nj-1))
      end do
      
!     Finally distribute the changes to be to the four bounding corner points, 
!     these receive the full change from the single cell of which they form one 
!     corner.
!     INSERT

      dnode(1,1) = dcell(1,1); dnode(ni,1) = dcell(ni-1,1)
      dnode(1,nj) = dcell(1,nj-1); dnode(ni,nj) = dcell(ni-1,nj-1)
      
!     Update the solution by adding the changes at the nodes "dnode" to the flow
!     property "prop"
!     INSERT

      prop = prop + dnode
      
      end subroutine sum_fluxes

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

      end module flux_stencil


